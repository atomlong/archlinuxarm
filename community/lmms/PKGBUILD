# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Lukas Fleischer <lfleischer@archlinux.org>
# Contributor: Shinlun Hsieh <yngwiexx@yahoo.com.tw>
# Contributor: Mateusz Herych <heniekk@gmail.com>
# Contributor: Storyteller <spiralsorrow@hotmail.com>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - remove wine makedepend and optdepend
#  - remove carla makedepend
#  - remove -DWINE_INCLUDE_DIR and -DWINE_LIBRARY from cmake command
#  - upstream rpmalloc fix for ARM

pkgname=lmms
pkgver=1.2.2
pkgrel=3
pkgdesc='The Linux MultiMedia Studio'
arch=('x86_64')
url="https://lmms.io"
license=('GPL2')
groups=('pro-audio')
depends=('gcc-libs' 'glibc' 'lame' 'libx11' 'libxcb' 'qt5-base' 'qt5-x11extras'
'sdl' 'sndio' 'zlib')
makedepends=('alsa-lib' 'bash-completion' 'cmake' 'doxygen'
'extra-cmake-modules' 'fftw' 'fltk' 'fluidsynth' 'freetype2' 'jack' 'ladspa' 'libgig'
'libogg' 'libpulse' 'libsamplerate' 'libsndfile' 'libvorbis' 'portaudio'
'qt5-tools' 'stk')
optdepends=('pulseaudio: pulseaudio support'
            'wine: VST plugin support')
source=("https://github.com/${pkgname}/${pkgname}/releases/download/v${pkgver}/${pkgname}_${pkgver}.tar.xz"
        'https://github.com/rampantpixels/rpmalloc/commit/1c580b394e1a20e63d63c72cad421f7be4422089.patch')
sha512sums=('df74d9e938f1c3807e9941b11db4ccfe9450e23b723c82774de15b7666ac39f1bfdd8519231e28849f994628190ecc92fa05d55bbc0b50a4421f2d183e729028'
            '95b8df46fbb1194d30ba9dd34e7b0325590c63a1a2f34730fa62c00fea1a75e728733ee3c287ed2b31150e66aa18e170fe4725a0e7cad2518039a19b12dc389c')
b2sums=('8b561068194e9a4af8260675e784c25a92b6b2f731c29b677cbc16581306bbadcf27ea529adbcd735ff4adffedf3dd98ec7b2d89428a63ea600d022ecdae58e4'
        '350a8aa6610e30b5bd2b56a854bc7e8f872064c510cca84a641a8fb666e1a2620a95604c43ea6979ab651cad83524180bdbeae7e70219b109e6740f4f48e54e6')

prepare() {
  mv -v "${pkgname}" "${pkgname}-${pkgver}"
  # upstream rpmalloc fix for ARM
  patch -p1 -d ${pkgname}-${pkgver}/src/3rdparty/rpmalloc/rpmalloc -i "${srcdir}/1c580b394e1a20e63d63c72cad421f7be4422089.patch"
}

build() {
  cd "${pkgname}-${pkgver}"
  export LDFLAGS="${LDFLAGS}"
  cmake -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_BUILD_TYPE='None' \
        -DWANT_QT5=ON \
        -DWANT_SOUNDIO=OFF \
        -DCMAKE_INSTALL_LIBDIR=lib \
        -DCMAKE_MODULE_PATH=/usr/share/ECM/find-modules \
        -DCMAKE_PREFIX_PATH=/usr/include/wine/windows \
        -DOpenGL_GL_PREFERENCE=GLVND \
        -W no-dev \
        -B build \
        -S .
  make VERBOSE=1 -C build
}

package() {
  depends+=('libasound.so' 'libfftw3f.so' 'libfltk.so' 'libfluidsynth.so'
  'libgig.so' 'libogg.so' 'libportaudio.so' 'libpulse.so' 'libstk-4.6.1.so'
  'libvorbis.so' 'libvorbisenc.so' 'libvorbisfile.so' 'libsamplerate.so')
  cd "${pkgname}-${pkgver}"
  make VERBOSE=1 DESTDIR="${pkgdir}" install -C build
}
